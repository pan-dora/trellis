buildscript {
}

plugins {
    id 'com.palantir.docker' version '0.13.0'
}

ext {
    vendor = 'pandorasystems'
    homepage = 'http://www.trellisldp.org'
    docURL = 'http://www.trellisldp.org/apidocs'
    license = 'Apache 2'
}

configure(allprojects) { project ->
    apply plugin: 'maven'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

     processResources {
        filesNotMatching(["**/*.sh", "**/*.jar", "**/*.yml"]) {
            expand(project.properties)
        }
    }

    def dockerResourceDir = "${rootProject.buildDir}/resources/main/docker"

    docker {
        name "${vendor}/trellisldp"
        tags "${version}"
        dockerfile file("${dockerResourceDir}/Dockerfile")
        files "${dockerResourceDir}/cfg","${dockerResourceDir}/lib"
        copySpec.from("${buildDir}/maven").into("${mavenInstallPath}")
        pull true
        noCache true
    }

    def homeDir = System.getProperty('user.home')
    def mavenLocal = new File(homeDir, ".m2/${mavenInstallPath}")

    task copyTask(type: Copy) {
        from mavenLocal
        into "${rootProject.buildDir}/maven"
    }
    copyTask.dependsOn install
}

configure(subprojects) { subproject ->
    task deleteSubprojectFiles(type: Delete) {
        delete "${projectDir}/settings.gradle", "${projectDir}/docs"
    }
}

configurations {
    buildToolsConfig
}

dependencies {
    buildToolsConfig 'org.trellisldp:trellis-build-tools:0.2.0'
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

task submoduleUpdate(type: Exec) {
    description = 'Updates and merges submodules'
    executable = 'git'
    args = ['submodule','update','--remote','--merge']
}

checkstyleMain.dependsOn processBuildTools

